PREFIX = "pages"

type Page(
    element,
    number,
    _loaded
)

impl new(self, number) for Page {
    self.number = number
    self._loaded = false
}

impl render(self, parent) for Page {
    if not self._loaded {
        self._loaded = true

        # create page
        @md = null
        if self.number.is(Num) {
            md = fetch("\(PREFIX)/page\(self.number).md")
        } else {
            md = fetch("\(PREFIX)/\(self.number).md")
        }
        if not md.ok {
            panic("Unable to fetch page\(self.number) markdown")
        }
        @html = bindings.markdown.makeHtml(md.text)
        self.element = $("div").new().setClass("page")
        self.element.clear()

        @text = $("div").new().setClass("page-text").setHtml(html)
        @code = text.children().1
        text.detach(code)

        # replace code with live
        @codeBlocks = text.$("pre > code").all()
        each block of codeBlocks {
            @code = Editor.cleanText(block.text())
            @parent = block.parent()
            parent.clear()
            @icode = $("div").new()
            parent.attach(icode)
            if code.startsWith("#!/raw") {
                code = code.substring("#!/raw\n".size()+1, code.size())
                createSnippet(icode, code, dict(raw: true))
                continue
            } elif code.startsWith("#!/run\n") {
                code = code.substring("#!/run\n".size()+1, code.size())
                createSnippet(icode, code, dict(run: true))
            } elif code.startsWith("#!/out\n") {
                code = code.substring("#!/out\n".size()+1, code.size())
                createSnippet(icode, code, dict(out: true))
            } else {
                createSnippet(icode, code, dict())
            }
        }

        # replace latex
        bindings.katex.render(text)


        @editor = $("div").new().setClass("page-editor")
        createEditor(editor, code.text())
        self.element.attach(text)
        self.element.attach(editor)
    }
    parent.element.clear()
    parent.element.attach(self.element)

    return self.element.children()
}
